group 'kotlinjs'
version '1.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.1.2-5'
    ext.webDir = 'web'
    repositories {
        maven { url 'http://192.168.2.247:8078' }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'java'
apply plugin: 'kotlin2js'

compileKotlin2Js {
    kotlinOptions.moduleKind = 'amd'
    kotlinOptions.sourceMap = true
}

build.doLast {
    configurations.compile.each { File file ->
        copy {
            includeEmptyDirs = false
            from zipTree(file.absolutePath) into "${projectDir}/${webDir}/lib"
            include { fileTreeElement ->
                def path = fileTreeElement.path
                path.endsWith(".js") && (path.startsWith("META-INF/resources/") || !path.startsWith("META-INF/"))
            }
        }
    }

    copy {
        File artifact = new File("build/libs/${project.name}-${project.version}.jar")
        includeEmptyDirs = false

        from zipTree(artifact)
        into "${webDir}"
        include { fileTreeElement ->
            def path = fileTreeElement.path
            !(path.startsWith("META-INF/") || path.startsWith("${project.name}"))
        }
    }
    // Copy scripts to web directory
    copy {
        includeEmptyDirs = false
        from new File("build/classes/main")
        into "${webDir}/lib"
    }

    copy {
        includeEmptyDirs = false
        from new File("build/classes/test")
        into "${webDir}/lib"
    }

    copy {
        includeEmptyDirs = false
        from new File("build/resources/test")
        into "${webDir}"
    }
}

clean.doFirst() {
    delete("${webDir}")
}

repositories {
    maven { url 'http://192.168.2.247:8078' }
    maven {url  "http://dl.bintray.com/kotlin/kotlinx.html/"}
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlin_version"
    compile 'org.jetbrains.kotlinx:kotlinx-html-js:0.6.3'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}
